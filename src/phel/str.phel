(ns phel\str
  (:require phel\core)
  (:use InvalidArgumentException))

(defn split
  "Splits string on a regular expression, returning a vector of parts."
  {:example "(split \"hello world foo bar\" #\"\\s+\") # => [\"hello\" \"world\" \"foo\" \"bar\"]"}
  [s re & [limit]]
  (values (php/preg_split re s (or limit -1))))

(defn chars
  "Returns a vector of characters from string s.

  This is a convenience function for converting strings to character sequences.
  Properly handles multibyte UTF-8 characters."
  {:example "(chars \"hello\") # => [\"h\" \"e\" \"l\" \"l\" \"o\"]"
   :see-also '[seq]}
  [s]
  (apply vector (php/mb_str_split s)))

(defn join
  "Returns a string of all elements in coll, separated by an optional separator."
  {:example "(join \", \" [\"apple\" \"banana\" \"cherry\"]) # => \"apple, banana, cherry\""}
  [separator & [coll]]
  (let [[coll separator] (if (nil? coll) [separator ""] [coll separator])]
    (php/implode separator (to-php-array coll))))

(defn subs
  "Returns the substring of s from start (inclusive) to end (exclusive)."
  {:example "(subs \"hello world\" 0 5) # => \"hello\""}
  [s start & [end]]
  (let [len (php/mb_strlen s)
        end (or end len)]
    (when (or (php/< start 0) (php/< end start) (php/> end len))
      (throw (php/new InvalidArgumentException "Invalid start or end index for subs")))
    (php/mb_substr s start (- end start))))

(defn reverse
  "Returns s with its characters reversed."
  {:example "(reverse \"hello\") # => \"olleh\""}
  [s]
  (let [chars (php/mb_str_split s)]
    (join (core/reverse chars))))

(defn repeat
  "Returns a string containing n copies of s."
  {:example "(repeat \"ha\" 3) # => \"hahaha\""}
  [s n]
  (php/str_repeat s n))

(defn replace
  "Replaces all instances of match with replacement in s."
  {:example "(replace \"hello world\" \"world\" \"there\") # => \"hello there\""}
  [s match replacement]
  (let [match (if (and
                   (> (php/mb_strlen match) 1)
                   (=
                    (php/substr match 0 1)
                    (php/substr match -1 1)))
                match
                (format "/%s/" (php/preg_quote match "/")))]
    (if (function? replacement)
      (php/preg_replace_callback match (fn [x]
                                         (let [x (values (php-array-to-map x))]
                                           (apply replacement x))) s)
      (php/preg_replace match replacement s))))

(defn replace-first
  "Replaces the first instance of match with replacement in s."
  {:example "(replace-first \"hello world world\" \"world\" \"there\") # => \"hello there world\""}
  [s match replacement]
  (let [match (if (and
                   (> (php/mb_strlen match) 1)
                   (=
                    (php/substr match 0 1)
                    (php/substr match -1 1)))
                match
                (format "/%s/" (php/preg_quote match "/")))]
    (if (function? replacement)
      (php/preg_replace_callback match (fn [x]
                                         (let [x (values (php-array-to-map x))]
                                           (apply replacement x))) s 1)
      (php/preg_replace match replacement s 1))))

(defn trim-newline
  "Removes all trailing newline or return characters from string."
  {:example "(trim-newline \"hello\\n\\n\") # => \"hello\""}
  [s]
  (loop [index (php/mb_strlen s)]
    (if (zero? index)
      ""
      (let [ch (php/mb_substr s (dec index) 1)]
        (if (or (= ch "\n") (= ch "\r"))
          (recur (dec index))
          (php/mb_substr s 0 index))))))

(defn capitalize
  "Converts first character to upper-case and all other characters to lower-case."
  {:example "(capitalize \"hELLO wORLD\") # => \"Hello world\""}
  [s]
  (let [first-char (php/mb_substr s 0 1)
        rest (php/mb_substr s 1)]
    (str (php/mb_strtoupper first-char)
         (php/mb_strtolower rest))))

(defn lower-case
  "Converts string to all lower-case."
  {:example "(lower-case \"HELLO World\") # => \"hello world\""}
  [s]
  (php/mb_strtolower s))

(defn upper-case
  "Converts string to all upper-case."
  {:example "(upper-case \"hello World\") # => \"HELLO WORLD\""}
  [s]
  (php/mb_strtoupper s))

(defn trim
  "Removes whitespace from both ends of string."
  {:example "(trim \"  hello  \") # => \"hello\""}
  [s]
  (php/preg_replace "/^[\s]+|[\s]+$/u" "" s))

(defn triml
  "Removes whitespace from the left side of string."
  {:example "(triml \"  hello  \") # => \"hello  \""}
  [s]
  (php/preg_replace "/^[\s]+/u" "" s))

(defn trimr
  "Removes whitespace from the right side of string."
  {:example "(trimr \"  hello  \") # => \"  hello\""}
  [s]
  (php/preg_replace "/[\s]+$/u" "" s))

(defn blank?
  "True if s is nil, empty, or contains only whitespace."
  {:example "(blank? \"   \") # => true"}
  [s]
  (if s
    (loop [index 0]
      (if (= (php/mb_strlen s) index)
        true
        (if (php/preg_match "/^\s$/u" (php/mb_substr s index 1))
          (recur (inc index))
          false)))
    true))

(defn starts-with?
  "True if s starts with substr."
  {:example "(starts-with? \"hello world\" \"hello\") # => true"}
  [s substr]
  (php/str_starts_with s substr))

(defn ends-with?
  "True if s ends with substr."
  {:example "(ends-with? \"hello world\" \"world\") # => true"}
  [s substr]
  (php/str_ends_with s substr))

(defn contains?
  "True if s contains substr."
  {:example "(contains? \"hello world\" \"lo wo\") # => true"}
  [s substr]
  (php/str_contains s substr))

(defn includes?
  "True if s includes substr."
  {:example "(includes? \"hello world\" \"world\") # => true"}
  [s substr]
  (php/str_contains s substr))

(defn re-quote-replacement
  "Escapes special characters in a replacement string for literal use."
  {:example "(re-quote-replacement \"$1.00\") # => \"\\$1.00\""}
  [replacement]
  (php/preg_replace "/([\\\$])/" "\$1" replacement))

(defn escape
  "Returns a new string with each character escaped according to cmap."
  {:example "(escape \"hello\" {\"h\" \"H\" \"o\" \"O\"}) # => \"HellO\""}
  [s cmap]
  (loop [index 0
         buffer ""]
    (if (= (php/mb_strlen s) index)
      buffer
      (let [ch (php/mb_substr s index 1)]
        (let [ch (or (cmap ch) ch)]
          (recur (inc index) (format "%s%s" buffer ch)))))))

(defn index-of
  "Returns the index of value in s, or nil if not found."
  {:example "(index-of \"hello world\" \"world\") # => 6"}
  [s value & [from-index]]
  (let [from-index (or from-index 0)
        from-index (let [abs (php/abs from-index)
                         v (min abs (dec (php/mb_strlen s)))]
                     (if (< from-index 0)
                       (* v -1)
                       v))
        result (php/mb_strpos s value from-index)]
    (if (= result false)
      nil
      result)))

(defn last-index-of
  "Returns the last index of value in s, or nil if not found."
  {:example "(last-index-of \"hello world world\" \"world\") # => 12"}
  [s value & [from-index]]
  (let [max-index (dec (php/mb_strlen s))
        from-index (or from-index max-index)
        from-index (let [abs (php/abs from-index)
                         v (min abs max-index)]
                     (if (< from-index 0)
                       (- max-index v)
                       v))]
    (loop [s s
           value value
           from-index from-index]
      (if (< from-index 0)
        nil
        (let [target (php/mb_substr s from-index)]
          (if (includes? target value)
            from-index
            (recur s value (dec from-index))))))))

(defn split-lines
  "Splits s on \\n or \\r\\n. Trailing empty lines are not returned."
  {:example "(split-lines \"hello\\nworld\\ntest\") # => [\"hello\" \"world\" \"test\"]"}
  [s]
  (split s "/\\r?\\n/"))

(defn pad-left
  "Returns a string padded on the left side to length len."
  {:example "(pad-left \"hello\" 10) # => \"     hello\""}
  [s len & [pad-str]]
  (php/str_pad s len (or pad-str " ") php/STR_PAD_LEFT))

(defn pad-right
  "Returns a string padded on the right side to length len."
  {:example "(pad-right \"hello\" 10) # => \"hello     \""}
  [s len & [pad-str]]
  (php/str_pad s len (or pad-str " ") php/STR_PAD_RIGHT))

(defn pad-both
  "Returns a string padded on both sides to length len."
  {:example "(pad-both \"hello\" 11) # => \"   hello   \""}
  [s len & [pad-str]]
  (php/str_pad s len (or pad-str " ") php/STR_PAD_BOTH))
