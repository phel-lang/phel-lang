(ns phel\test
  (:use Phel\Lang\Symbol)
  (:use Phel\Runtime\RuntimeFactory)
  (:use Phel\Compiler\Emitter\OutputEmitter\Munge)
  (:use Phel\Exceptions\TextExceptionPrinter)
  (:use Throwable))

# ------
# Report
# ------

(def stats @{
  :failed []
  :counts @{
    :failed 0
    :error 0
    :pass 0
    :total 0
  }
})

(defn report [data]
  (let [@{:state state :type type} data
        ok (= state :pass)]
    (print
      (case state
        :failed "F"
        :pass "."
        :error "E"))
    (update-in stats [:counts :total] inc)
    (update-in stats [:counts state] inc)
    (when (= (% (get-in stats [:counts :total]) 80) 0)
      (println))
    (when-not ok
      (update-in stats [:failed] push data))))


# ---------------------------
# `is` macro and it's asserts
# ---------------------------

(defn- location [form]
  (let [loc (php/-> form (getStartLocation))]
    (when loc
      @{:file (php/-> loc (getFile))
        :line (php/-> loc (getLine))
        :column (php/-> loc (getColumn))})))

(defn- assert-predicate [form message negated]
  (let [pred (first form)
        value (second form)
        value-evaluated (gensym)
        type (if negated :predicate :predicate-not)
        loc (location form)
        result (gensym)
        report-data (gensym)]
    `(let [,value-evaluated ,value
           ,result (,pred ,value-evaluated)
           ,result (if ,negated (not ,result) ,result)
           ,report-data @{:type ,type
                          :message ,message
                          :location ,loc
                          :pred ',pred
                          :value ',value
                          :form ',form
                          :result ,value-evaluated}]
      (if ,result
        (report (merge ,report-data @{:state :pass}))
        (report (merge ,report-data @{:state :failed}))))))

(defn- assert-binary [form message negated]
  (let [pred (first form)
        expected (second form)
        actual (second (next form))
        type (if negated :binary :binary-not)
        loc (location form)
        expected-evaluated (gensym)
        actual-evaluated (gensym)
        result (gensym)
        report-data (gensym)]
    `(let [,expected-evaluated ,expected
           ,actual-evaluated ,actual
           ,result (,pred ,expected-evaluated ,actual-evaluated)
           ,result (if ,negated (not ,result) ,result)
           ,report-data @{:type ,type
                          :message ,message
                          :location ,loc
                          :pred ',pred
                          :expected ,expected-evaluated
                          :actual ',actual
                          :form ',form
                          :result ,actual-evaluated}]
      (if ,result
        (report (merge ,report-data @{:type :binary :state :pass}))
        (report (merge ,report-data @{:type :binary :state :failed}))))))

(defn- assert-output [form message]
  (let [expected (second form)
        body (second (next form))]
    (assert-binary `(= ,expected (with-output-buffer ,body)) message false)))

(defn- assert-thrown [form message]
  (let [exception-symbol (second form)
        body (nnext form)
        loc (location form)
        report-data (gensym)
        e (gensym)]
    `(let [,report-data @{:type :thrown
                          :location ,loc
                          :form ',form
                          :exception-symbol ,(str exception-symbol)
                          :message ,message}]
      (try
        ,@body
        (report (merge ,report-data @{:state :failed}))
        (catch ,exception-symbol ,e
          (report (merge ,report-data @{:state :pass})))))))

(defn- assert-thrown-with-msg [form message]
  (let [exception-symbol (second form)
        expected-message (second (next form))
        body (next (nnext form))
        loc (location form)
        actual-message (gensym)
        report-data (gensym)
        e (gensym)]
    `(let [,report-data @{:type :thrown-with-msg
                          :form ',form
                          :location ,loc
                          :exception-symbol ,(str exception-symbol)
                          :expected-message ,expected-message
                          :message ,message}]
      (try
        ,@body
        (report (merge ,report-data @{:state :failed :actual-message nil}))
        (catch ,exception-symbol ,e
          (let [,actual-message (php/-> ,e (getMessage))]
            (if (= ,expected-message ,actual-message)
              (report (merge ,report-data @{:state :pass :actual-message ,actual-message}))
              (report (merge ,report-data @{:state :failed :actual-message ,actual-message})))))))))

(defn- assert-any [form message]
  (let [loc (location form)
        result (gensym)
        report-data (gensym)]
    `(let [,result ,form
           ,report-data @{:type :any
                          :message ,message
                          :location ,loc
                          :form ',form
                          :result ,result}]
      (if ,result
        (report (merge ,report-data @{:state :pass}))
        (report (merge ,report-data @{:state :failed}))))))

(defn- assert-expr [form message]
  (cond
    (and (tuple? form) (= (first form) 'not) (tuple? (second form)) (= 3 (count (second form))))
      (assert-binary form message true)
    (and (tuple? form) (= (first form) 'not) (tuple? (second form)) (= 2 (count (second form))))
      (assert-predicate form message true)
    (and (tuple? form) (= (first form) 'thrown?))
      (assert-thrown form message)
    (and (tuple? form) (= (first form) 'thrown-with-msg?))
      (assert-thrown-with-msg form message)
    (and (tuple? form) (= (first form) 'output?))
      (assert-output form message)
    (and (tuple? form) (= 3 (count form)))
      (assert-binary form message false)
    (and (tuple? form) (= 2 (count form)))
      (assert-predicate form message false)
    (assert-any form message)
  ))

(defmacro is [form & [message]]
  (let [loc (location form) e (gensym)]
    `(try ,(assert-expr form message)
      (catch Throwable ,e
        (report @{:state :error :location ,loc :message ,message :form ',form :exception ,e})))))

# --------------
# Defining tests
# --------------

(defmacro deftest [name & body]
  `(defn ,name @{:test true} []
      ,@body
      ))

# ---------------------
# error/failure printer
# ---------------------

(defn- print-headline [prefix @{:message message :location location}]
  (print prefix)
  (when message
    (print (str " in '" message "'")))
  (when location
    (print (str " (" (php/realpath (get location :file)) ":" (get location :line) ")")))
  (println))

(defn- print-failure-headline [data]
  (print-headline "Failure" data))

(defn- print-error-headline [data]
  (print-headline "Error" data))

(defn- print-error [data]
  (print-error-headline data)
  (let [@{:exception exception :form form} data
        printer (php/:: TextExceptionPrinter (readableWithStyle))]
    (println "              Test:" form)
    (println " throwed exception:" (php/get_class exception))
    (println)
    (php/-> printer (printStackTrace exception))))

(defn- print-predicate-failure [@{:pred pred :value value :result result}]
  (println "                 Test:" value)
  (println "         evaluated to:" result)
  (println "  but doesn't satisfy:" pred))

(defn- print-predicate-not-failure [@{:pred pred :value value :result result}]
  (println "              Test:" value)
  (println "      evaluated to:" result)
  (println "  but does satisfy:" pred " (it shouldn't)"))

(defn- print-binary-failure [@{:pred pred :expected expected :actual actual :result result}]
  (println "          Test:" actual)
  (println "  evaluated to:" result)
  (println "    but is not:" pred "to" expected))

(defn- print-binary-not-failure [@{:pred pred :expected expected :actual actual :result result}]
  (println "          Test:" actual)
  (println "  evaluated to:" result)
  (println "        but is:" pred "to" expected "(it shouldn't be)"))

(defn- print-thrown-failure [@{:form form :exception-symbol exception-symbol}]
  (println "    expected:" form)
  (println "  to throw a:" exception-symbol "(it didn't)"))

(defn- print-thrown-with-msg-failure [@{:form form :exception-symbol exception-symbol :expected-message expected-message :actual-message actual-message}]
  (if (nil? actual-message)
    (do
      (println "    expected:" form)
      (println "  to throw a:" exception-symbol "(it didn't)")
    (do
      (println "      expected:" form)
      (println "    to throw a:" exception-symbol)
      (println "  with message:" expected-message)
      (println "       but got:" actual-message)))))

(defn- print-any-failure [@{:form form :result result}]
  (println "          Test:" form)
  (println "  evaluated to:" result "(which is not truthy)"))

(defn- print-failure [data]
  (print-failure-headline data)
  (case (get data :type)
    :predicate (print-predicate-failure data)
    :predicate-not (print-predicate-not-failure data)
    :binary (print-binary-failure data)
    :binary-not (print-binary-not-failure data)
    :thrown (print-thrown-failure data)
    :thrown-with-msg (print-thrown-with-msg-failure data)
    :any (print-any-failure data)))

(defn- print-failure-or-error [data]
  (case (get data :state)
    :error (print-error data)
    :failed (print-failure data)))

(defn print-summary []
  (println)
  (println)
  (for [data :in (get stats :failed)]
    (println "~~~~~~~~~~")
    (print-failure-or-error data))

  (println)
  (println)

  (let [@{:failed failed :error error :pass pass} (get stats :counts)
        total (+ failed error pass)]
    (println "Passed:" pass)
    (println "Failed:" failed)
    (println "Error:" error)
    (println "Total:" total)))

# -------------
# Running tests
# -------------

(defn- find-test-fns [ns]
  (let [munge (php/new Munge)
        munge-ns (php/-> munge (encodeNs ns))]
    (for [fn-name :keys (get-in php/$GLOBALS ["__phel" munge-ns])
          :let [meta (get-in php/$GLOBALS ["__phel_meta" munge-ns fn-name])]
          :when (get meta :test)]
      (get-in php/$GLOBALS ["__phel" munge-ns fn-name]))))

(defn- run-test [ns]
  (let [rt (php/:: \Phel\Runtime\RuntimeFactory (getInstance))
        _ (php/-> rt (loadNs ns))
        tests (find-test-fns ns)
        ]
    (dofor [test :in tests]
      (test))))

(defn run-tests [& namespaces]
  (dofor [n :in namespaces
          :let [s (php/-> n (getName))]]
    (run-test s))
  (print-summary))

(defn successful? []
  (let [@{:failed failed :error error} (get stats :counts)]
    (zero? (+ failed error))))
