(ns phel\debug
  (:require phel\core :refer [print-str str gensym])
  (:require phel\str :as s))

(def- *trace-level* (var 0))
(def- *trace-id* (var 0))
(def- *max-trace-digits* (var 2)) ; default to 2 digits unless overridden

(defn set-trace-id-padding!
  "Sets the number of digits for trace ID padding.

  Example:
    (set-trace-id-padding! 3)
    # Uses 3 digits: 001, 002, etc."
  [estimated-id-padding]
  (swap! *max-trace-digits* (fn [_] estimated-id-padding)))

(defn- next-id []
  (swap! *trace-id* inc))

(defn dotrace
  "Wraps a function to print each call and result with indentation.

  Example:
    (def add (dotrace \"add\" +))
    (add 2 3)
    # Prints: TRACE t01: (add 2 3)
    # Prints: TRACE t01: => 5"
  [name f]
  (fn [& args]
    (let [id (next-id)
          id-str (s/pad-left (str id) (deref *max-trace-digits*) "0")
          indent (s/repeat "|    " (deref *trace-level*))
          arg-str (if (empty? args)
                    ""
                    (str " " (s/join " " (map print-str args))))
          call-str (str "(" name arg-str ")")]
      (println (str "TRACE t" id-str ": " indent call-str))
      (swap! *trace-level* inc)
      (let [res (apply f args)]
        (swap! *trace-level* dec)
        (println (str "TRACE t" id-str ": " indent "=> " res))
        res))))

(defn reset-trace-state!
  "Resets trace counters to initial values.

  Example:
    (reset-trace-state!)
    # Trace IDs restart from 01"
  []
  (do
    (set! *trace-level* 0)
    (set! *trace-id* 0)
    (set! *max-trace-digits* 2)))

(defmacro dbg
  "Evaluates an expression and prints it with its result.

  Example:
    (dbg (+ 1 2))
    # Prints: (+ 1 2) => 3
    # => 3"
  [expr]
  (let [res (gensym)]
    `(let [,res ,expr]
       (println (str (print-str (quote ,expr)) " => " (print-str ,res)))
       ,res)))

(defmacro spy
  "Evaluates an expression and prints it with an optional label.

  Example:
    (spy (+ 1 2))
    # Prints: SPY (+ 1 2) => 3
    # => 3"
  ([expr]
   (let [res (gensym)]
     `(let [,res ,expr]
        (println (str "SPY " (print-str (quote ,expr)) " => " (print-str ,res)))
        ,res)))
  ([label expr]
   (let [res (gensym)
         label-val (gensym)]
     `(let [,label-val ,label
            ,res ,expr]
        (println (str "SPY " (print-str ,label-val) " => " (print-str ,res)))
        ,res))))

(defn tap
  "Prints a value and returns it unchanged. Useful in pipelines.

  Example:
    (-> 5 (tap) (* 2))
    # Prints: TAP => 5
    # => 10"
  ([value]
   (println (str "TAP => " (print-str value)))
   value)
  ([value handler]
   (handler value)
   value))
