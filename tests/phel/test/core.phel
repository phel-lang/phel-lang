(ns phel-test\test\core
  (:require phel\test :refer [deftest is])
  (:use Phel\Lang\Symbol))

# -------------
# Destructuring
# -------------

(deftest destructure-vector
  (is (= 3 (let [[a b] [1 2]] (+ a b))) "from vector")
  (is (= 3 (let [[a b] (php/array 1 2)] (+ a b))) "from php array")
  (is (= 10 (let [[a [c d] b] [1 (list 4 3) 2]] (+ a b c d))) "nested")
  (is (= 4 (let [[a _ b] [1 2 3]] (+ a b))) "ignore value"))

(deftest destructure-hash-map
  (is (= 3 (let [{:a a :b b} {:a 1 :b 2}] (+ a b))) "destructure hash map")
  (is (= 6 (let [{:a [a1 a2] :b b} {:a [1 3] :b 2}] (+ a1 a2 b))) "nested destructure hash map"))

# ----------------------------
# Basic methods for quasiquote
# ----------------------------

(deftest test-next
  (is (nil? (next [])) "next of empty vector")
  (is (nil? (next [1])) "next of one element vector")
  (is (= [2] (next [1 2])) "next of two element vector"))

(deftest test-concat
  (is (= [1 2] (concat [1 2])) "concat one argument")
  (is (= [1 2 3 4] (concat [1 2] [3 4])) "concat two arguments")
  (is (= [1 2 3 4] (concat [1] [2 3] [4])) "concat three arguments")
  (is (= '() (concat nil)) "concat nil")
  (is (= '() (concat nil nil)) "concat two nil")
  (is (= [1 2 3 4 5 6] (concat [1 2 3] nil [4 5 6])) "concat vectors and nil"))

(deftest test-set-push
  (let [s1 (set 1 2)
        s2 (push s1 3)]
    (is (= (set 1 2 3) s2) "set push"))
  (let [s1 (set 1 2)
        s2 (push s1 2)]
    (is (= (set 1 2) s2) "set push existing value")))

(deftest test-set-concat
  (is (= (set 0 1 2 3) (concat (set 1 2) [0 3])) "set concat vector")
  (is (= (set 0 1 2 3) (concat (set 1 2) [0 1 2 3])) "set concat vector with common values")
  (is (= (set 0 1 2) (concat (set 1 2) (set 0 1))) "set concat"))

(defstruct my-struct [a b c])

(deftest test-struct
  (let [x (my-struct 1 2 3)]
    (is (= :struct (type x)) "struct: common type")
    (is (true? (struct? x)) "struct: common type test")
    (is (= 1 (get x :a)) "struct: get value from struct")
    (is (= (my-struct 12 2 3) (put x :a 12)) "struct: put value on struct")
    (is (true? (my-struct? x)) "struct: correct type")
    (is (false? (my-struct? :a)) "struct: incorrect type")))

(deftest test-struct-with-types
  (let [x (my-struct true 1 "str" {:a 1} [:b :c])]
    (is (= x (my-struct true 1 "str" {:a 1} [:b :c])) "structs with same data")))

(deftest test-__FILE__
  (is (true? (>= (php/strpos __FILE__ "tests/phel/test/core.phel") 0)) "__FILE__"))

(deftest test-__DIR__
  (is (true? (and (false? (php/strpos __DIR__ "tests/phel/test/core.phel")) (>= (php/strpos __DIR__ "tests/phel/test") 0))) "__DIR__"))


(deftest test-var
  (is (= :var (type (var 10))) "type of var is :var")
  (is (= 10 (deref (var 10))) "deref variable")
  (is (true? (var? (var 10))) "var? return true if x is a variable")
  (is (false? (var? 10)) "var? return false if x is a number")
  (is (= 20 (let [x (var 10)] (set! x 20) (deref x))) "set new value to variable")
  (is (= 11 (let [x (var 10)] (swap! x + 1) (deref x))) "swap a value by incrementing the number"))

(deftest test-time
  (is (= 2 (+ 1 1)) "time returns expr value")
  (let [output-print (with-output-buffer (time (+ 1 1)))]
    (is (string? output-print) "time prints to output")
    (is (str-contains? output-print "Elapsed time:"))
    (is (str-contains? output-print "msecs"))))

(deftest test-name
  (is (= "string" (name "string")) "name on string")
  (is (= "symbol" (name 'symbol)) "name on symbol")
  (is (= "keyword" (name :keyword)) "name of keyword"))

(deftest test-namespace
  (is (= nil (namespace 'symbol)) "namespace on symbol without ns")
  (is (= nil (namespace :keyword)) "namespace of keyword without ns")
  (is (= "test" (namespace (php/:: Symbol (createForNamespace "test" "symbol")))) "namespace on symbol with ns")
  (is (= "phel-test\\test\\core" (namespace ::keyword)) "namespace of keyword with ns"))

(deftest test-full-name
  (is (= "string" (name "string")) "full-name on string")
  (is (= "symbol" (full-name 'symbol)) "full-name on symbol without ns")
  (is (= "keyword" (full-name :keyword)) "full-name of keyword without ns")
  (is (= "test/symbol" (full-name (php/:: Symbol (createForNamespace "test" "symbol")))) "full-name on symbol with ns")
  (is (= "phel-test\\test\\core/keyword" (full-name ::keyword)) "full-name of keyword with ns"))

(deftest test-read-string
  (is (= '(+ 1 1) (read-string "(+ 1 1)")) "read simple expression")
  (is (= nil (read-string "")) "read empty string"))

(deftest test-eval-form
  (is (= 2 (eval '(+ 1 1))) "eval simple expression"))

(deftest test-compile-form
  (is (= "(1 + 1);" (compile '(+ 1 1))) "compile simple expression"))
